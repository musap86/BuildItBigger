// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.google.guava:guava:22.0'
        classpath 'com.android.tools.build:gradle:3.1.2'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

ext {
    supportLibVersion = '26.1.0'
    playServicesVersion = '15.0.1'
    googleApiVersion = '1.23.0'
    findBugsVersion = '3.0.1'
    testVersion = '1.0.1'
    espressoIntentsVersion = '3.0.1'
    espressoCoreVersion = '2.2.2'
    jUnitVersion = '4.12'
}

task serverTestStart{
    group = 'server'
    dependsOn ':backend:appengineStart'

    doLast {
        println "Server is started."
    }
}

task serverTest{
    group = 'server'
    dependsOn ':app:connectedAndroidTest'
    mustRunAfter serverTestStart

    doLast {
        println "Tests are finished."
    }
}

task serverTestStop{
    group = 'server'
    dependsOn ':backend:appengineStop'
    mustRunAfter serverTest

    doLast {
        println "Server is stopped."
    }
}

task executeServerTests{
    group = 'server'
    description = 'This task:\n' +
            '1. Launches the GCE local development server\n' +
            '2. Runs all tests\n' +
            '3. Shuts the server down again'

    dependsOn serverTestStart,serverTest,serverTestStop

    doLast {
        println "Server testing is all done."
    }
}